set   autoscale                        # scale axes automatically
unset log                              # remove any log-scaling
unset label                            # remove any previous labels
set xtic auto                         	# set xtics automatically
set ytic auto                         # set ytics automatically
set ztic auto


set grid ytics lt 0.1 lw 0.1 lc rgb "#bbbbbb"
set grid xtics lt 0.1 lw 0.1 lc rgb "#bbbbbb"

#set terminal png size 2048,1356 enhanced font "Helvetica,20"
set terminal png size 1024,768 enhanced font "Helvetica,20"

set key left top


set xlabel "iteration x1000"
set ylabel "score value"

set output 'rl_progress_training.png'
plot 'dqn/summary_training_score.log' using 1:2 title 'DQN'  with lines lw 2.0 lc 'green', \
     'dqn/summary_training_score.log' every 10::0 using 1:3:2:2:4 notitle with candlesticks lw 2.0 lc 'green', \
     'dqn_priority/summary_training_score.log' using 1:2 title 'DQN priority'  with lines lw 2.0 lc 'blue', \
     'dqn_priority/summary_training_score.log' every 10::1 using 1:3:2:2:4 notitle with candlesticks lw 2.0 lc 'blue', \
     'dueling_dqn_priority/summary_training_score.log' using 1:2 title 'Dueling DQN priority'  with lines lw 2.0 lc 'red', \
     'dueling_dqn_priority/summary_training_score.log' every 10::2 using 1:3:2:2:4 notitle with candlesticks lw 2.0 lc 'red'

set key right top

set xlabel "value"
set ylabel "relative count"

set output 'rl_score_training_histogram.png'
plot 'dqn/summary_training_histogram.log' using 2:4 title 'DQN'  with boxes lw 2.0 lc 'green', \
     'dqn_priority/summary_training_histogram.log' using 2:4 title 'DQN priority'  with boxes lw 2.0 lc 'blue', \
     'dueling_dqn_priority/summary_training_histogram.log' using 2:4 title 'Dueling DQN priority'  with boxes lw 2.0 lc 'red'




set key left top

set xlabel "iteration"
set ylabel "score value"

set output 'rl_progress_testing.png'
plot  'dqn/summary_testing_score.log' using 1:2 title 'DQN'  with lines lw 2.0 lc 'green', \
      'dqn/summary_testing_score.log' every 1000::0 using 1:3:2:2:4 notitle with candlesticks lw 2.0 lc 'green', \
      'dqn_priority/summary_testing_score.log' using 1:2 title 'DQN priority'  with lines lw 2.0 lc 'blue', \
      'dqn_priority/summary_testing_score.log' every 1000::100 using 1:3:2:2:4 notitle with candlesticks lw 2.0 lc 'blue', \
      'dueling_dqn_priority/summary_testing_score.log' using 1:2 title 'Dueling DQN priority'  with lines lw 2.0 lc 'red', \
      'dueling_dqn_priority/summary_testing_score.log' every 1000::200 using 1:3:2:2:4 notitle with candlesticks lw 2.0 lc 'red'

set key right top

set xlabel "value"
set ylabel "relative count"

set output 'rl_score_testing_histogram.png'
plot  'dqn/summary_testing_histogram.log' using 2:4 title 'DQN'  with boxes lw 2.0 lc 'green', \
      'dqn_priority/summary_testing_histogram.log' using 2:4 title 'DQN priority'  with boxes lw 2.0 lc 'blue', \
      'dueling_dqn_priority/summary_testing_histogram.log' using 2:4 title 'Dueling DQN priority'  with boxes lw 2.0 lc 'red'
